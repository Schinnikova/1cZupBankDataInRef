Функция СоздатьДополнительныеРеквизиты() Экспорт
	
	Успешно = Ложь;
	ТаблицаДопРеквизитов = Новый ТаблицаЗначений;
	Строка100 = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(100));
	ТаблицаДопРеквизитов.Колонки.Добавить("Имя", Строка100);
	ТаблицаДопРеквизитов.Колонки.Добавить("Наименование", Строка100);
	ТаблицаДопРеквизитов.Колонки.Добавить("Длина", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(4)));
	
	ЗаполнитьЗначенияСвойств(ТаблицаДопРеквизитов.Добавить(), Новый Структура("Имя, Наименование, Длина", "БанкОрг_БанкСчетНомер", "Номер счета", 20));
	ЗаполнитьЗначенияСвойств(ТаблицаДопРеквизитов.Добавить(), Новый Структура("Имя, Наименование, Длина", "БанкОрг_БанкСчетНаимБанка", "Банк", 250));
	ЗаполнитьЗначенияСвойств(ТаблицаДопРеквизитов.Добавить(), Новый Структура("Имя, Наименование, Длина", "БанкОрг_БанкСчетКоррСчетБанка", "Корреспондентский счет банка", 20));
	ЗаполнитьЗначенияСвойств(ТаблицаДопРеквизитов.Добавить(), Новый Структура("Имя, Наименование, Длина", "БанкОрг_БанкСчетБИКБанка", "БИК", 9));

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблРекв.Имя КАК Имя,
		|	ТаблРекв.Наименование КАК Наименование,
		|	ТаблРекв.Длина КАК Длина
		|ПОМЕСТИТЬ ВТ_ТаблРекв
		|ИЗ
		|	&ТаблРекв КАК ТаблРекв
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ДополнительныеРеквизитыИСведения.Ссылка, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка)) КАК Ссылка,
		|	ВТ_ТаблРекв.Имя КАК Имя,
		|	ВТ_ТаблРекв.Наименование КАК Наименование,
		|	ВТ_ТаблРекв.Длина КАК Длина
		|ИЗ
		|	ВТ_ТаблРекв КАК ВТ_ТаблРекв
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
		|		ПО ВТ_ТаблРекв.Имя = ДополнительныеРеквизитыИСведения.Имя
		|			И НЕ ДополнительныеРеквизитыИСведения.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ТаблРекв", ТаблицаДопРеквизитов); 
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Ссылка)Тогда
			Продолжить;
		КонецЕсли;
		СоздатьДополнительныйРеквизит(ВыборкаДетальныеЗаписи.Имя, ВыборкаДетальныеЗаписи.Наименование, ВыборкаДетальныеЗаписи.Длина);
		Успешно = Истина;
	КонецЦикла;          
	
	Возврат Успешно;

КонецФункции 

Процедура СоздатьДополнительныйРеквизит(Имя, Наименование, Длина)

	Константы.ИспользоватьДополнительныеРеквизитыИСведения.Установить(Истина);
	
	Параметры = УправлениеСвойствами.ПараметрыДобавленияСвойства();
	
	Параметры.Имя = Имя;
	Параметры.Наименование = Наименование;
	Параметры.Тип = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(Длина));
	Параметры.Вставить("ТипЗначения", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(Длина)));
	
	УправлениеСвойствами.ДобавитьСвойство(Метаданные.Справочники.Организации, Параметры);
	
КонецПроцедуры
